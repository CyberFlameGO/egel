# A prime sieve through continuations/callbacks.
#
# Generators through three primitives:
# yield n k gen     - yield n to k and proceed with gen
# step  gen k       - step generator gen then continue with k
# run   k   gen     - run consumer k on producer gen


import "prelude.eg"
import "io.ego"

using System
using IO

def yield = [ N K GEN -> K N GEN ]

def step = [ GEN K -> GEN K ]

def run = [ K GEN -> GEN K ]

def fix = [ F -> F [ X -> (fix F) X ] ]

def nums = 
    let NUMS = fix [ NUMS N K -> yield N K (NUMS (N+1)) ] in NUMS 2

def filter =
    [ F GEN K -> step GEN
        [ N GEN -> if F N then yield N K (filter F GEN) else (filter F GEN K)  ] ]

def sieve =
    [ GEN K -> step GEN
        [ N GEN -> yield N K (sieve (filter [ X -> not (X % N == 0) ] GEN)) ] ]
        
def print_all =
    [ N GEN -> print N "\n"; step GEN print_all ]

def main =
    run print_all (sieve nums)
